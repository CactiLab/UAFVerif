(**********************************)

set reconstructTrace = false.
set traceDisplay = none.
set verboseClauses = none.
set explainDerivation = false.
set reconstructDerivation = false.
set displayDerivation = false.
set abbreviateClauses = false.
set verboseEq = false.
set verboseTerm = false.
(********************************************************************)
(*                           Messages Type declaration                                        	    *)
(********************************************************************)
type msg.
type Atype. (* indicate the type of the autheticator *)
type Ltype. (* indicate if it is the first time login or step-up authentication *)
type Ftype. (* indicate if the appid is NULL or not *)
type Scene.

type Uname.
type Appid.
type Facetid.
type Callerid.
type PersonaID.
type SData.
type Chlg.
type Fchlg.
type Tr.
type Token.
type AAID.
type CNTR.
type Nonce.
type KeyID.

(********************************************************************)
(*                                  Symmetric encryption     		                         	        *)
(********************************************************************)
type key.
type Senc_alg.
const WeakSenc:Senc_alg[private].

fun senc(bitstring,key):KeyID.
reduc forall m: bitstring, k:key; sdec(senc(m,k),k) = m.

fun senc_msg(msg,key):msg.
reduc forall m: msg, k:key; sdec_msg(senc_msg(m,k),k) = m.

(********************************************************************)
(*                                      Signature function                                                *)
(********************************************************************)
type spkey.
type sskey.

fun spk(sskey):spkey.
fun sign(bitstring, sskey): bitstring.
reduc forall m: bitstring, ssk: sskey; checksign(sign(m,ssk),spk(ssk)) = m.
reduc forall m: bitstring, ssk: sskey; getmess(sign(m,ssk)) = m.



(********************************************************************)
(*                                           Hash Functions.   					                        *)
(********************************************************************)

fun hash(bitstring): bitstring.
fun hash_a(Appid):bitstring.
fun hash_tr(Tr): Tr.

(********************************************************************)
(*              Final Challenge Params function to compute a FCP params               *)
(********************************************************************)

fun FCParams(Appid,Facetid,Chlg,bitstring):bitstring[data].

(********************************************************************)
(*                                     Get TLSDATA from channel               	                *)
(********************************************************************)

fun GetTLSdata(channel):bitstring.
  
(********************************************************************)
(*                         Increasing counter value                                                    *)
(********************************************************************)

fun Incr(CNTR):CNTR.
reduc forall cntr:CNTR; ReIncr(Incr(cntr)) = cntr.

(********************************************************************)
(*                 Get KHAccess token from a random ASMtoken                           *)
(********************************************************************)

fun To_12b_token(Appid,bitstring,Callerid,PersonaID):Token.
fun To_12r_token(Appid):Token.
fun f1(Token,Appid):Token.

(********************************************************************)
(*  Get final challenge hash from the random hash value of the final challenge *)
(********************************************************************)

fun ToFc(bitstring):Fchlg.

(********************************************************************)
(*                                            facetid to appid                                                *)
(********************************************************************)
fun facetid_to_appid(Facetid):Appid.

(********************************************************************)
(*                                            gen_skAU                                                *)
(********************************************************************)
fun gen_skAU(sskey,bitstring):sskey.
fun gen_cntr(CNTR,bitstring):CNTR.
fun gen_kid(KeyID,bitstring):KeyID.
(********************************************************************)
(*                                  table for database                                                    *)
(********************************************************************)

table AppList(Appid,Facetid).
table AuthAppList(Appid,Facetid).
table ASMDB(Appid,KeyID,KeyID).
table TrustCallerid(Callerid).
table AutrDB(Appid,KeyID,KeyID).
(********************************************************************)
(*                                   channel declarations                                               *)
(********************************************************************)
free c:channel.


(********************************************************************)
(*                                   const declarations                                                   *)
(********************************************************************)
const autr_1b:Atype.
const autr_2b:Atype.
const autr_1r:Atype.
const autr_2r:Atype.
const empty:Ltype.
const stepup:Ltype.
const null:Ftype.
const hasset:Ftype.
const software:Scene.
const hardware:Scene.
const debug:bitstring[private].
const reg_cntr:CNTR[private].
const auth_cntr:CNTR[private].

(********************************************************************)
(*                                               Event                                                          *)
(********************************************************************)
event RP_success_reg(Uname,Appid,AAID,KeyID).
event UA_init_reg(Uname).
event Autr_verify_reg(Uname,Appid,AAID,KeyID).

event malicious_RP_to_US().
event malicious_US_to_RP().
event malicious_RP_to_UA().
event malicious_UC_to_UA().
event malicious_UA_to_RP().
event malicious_UA_to_UC().
event malicious_ASM_to_UC().
event malicious_UC_to_ASM().
event malicious_Autr_to_ASM().
event malicious_ASM_to_Autr().
event leak_token().
event leak_kw().
event leak_skat().
event leak_skau().
event leak_cntr().
event leak_kid().

(********************************************************************)
(*                              Registration message format                                        *)
(********************************************************************)
fun InputUandP(Uname, bitstring):msg[data].
fun RegRSbegin(Uname, bitstring):msg[data].
fun RegSRreq(Uname,Appid,SData,Chlg):msg[data].
fun RegSRreq_null(Uname,SData,Chlg):msg[data].
fun RegRSresp(SData,AAID,Fchlg,KeyID,CNTR,spkey,spkey,bitstring,bitstring):msg[data].
fun RegRUreq(Uname,Appid,SData,Chlg):msg[data].
fun RegRUreq_null(Uname,SData,Chlg):msg[data].
fun RegURresp(SData,AAID,Fchlg,KeyID,CNTR,spkey,spkey,bitstring,bitstring):msg[data].
fun RegUCreq(Uname,Appid,SData,Chlg,bitstring):msg[data].
fun RegUCreq_null(Uname,SData,Chlg,bitstring):msg[data].
fun RegCUresp(SData,AAID,Fchlg,KeyID,CNTR,spkey,spkey,bitstring,bitstring):msg[data].
fun RegCMreq(Uname,Appid,bitstring):msg[data].
fun RegMCresp(AAID,Fchlg,KeyID,CNTR,spkey,spkey,bitstring):msg[data].
fun RegMAreq(Uname,Appid,Token,Fchlg):msg[data].
fun RegAMresp_12b(AAID,Fchlg,KeyID,CNTR,spkey,spkey,KeyID,bitstring):msg[data].
fun RegAMresp_12r(AAID,Fchlg,KeyID,CNTR,spkey,spkey,bitstring):msg[data].

(********************************************************************)
(*                                            FIDO Server                                                 *)
(* 1. RegServer to registration                                                                      *)
(* 2. AuthServer to authentication                                                                 *)
(********************************************************************)

let RegUS_seta(SR:channel, appid:Appid) =
(
	in(SR, x:msg);
    let RegRSbegin(uname, tlsdata) = x in
    new sdata:SData;
	new chlg:Chlg;
	out(SR, RegSRreq(uname,appid,sdata,chlg));
	in(SR,m:msg);
	let RegRSresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s) = m in
	let FCParams(=appid,facetid,=chlg,=tlsdata) = fcp in
	get AppList(=appid,=facetid) in
	if xsdata = sdata && fc = ToFc(hash(fcp)) then
	if (aaid,fc,kid,cntr,pkAU) = checksign(s,pkAT) then
	(
		event RP_success_reg(uname,appid,aaid,kid);
		out(c,debug)
	)	
).

let RegUS_noa(SR:channel, appid:Appid) =
(   
    in(SR, x:msg);
    let RegRSbegin(uname, tlsdata) = x in
    new sdata:SData;
	new chlg:Chlg;
    out(SR, RegSRreq_null(uname,sdata,chlg));
    in(SR,m:msg);
    let RegRSresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s) = m in
    let FCParams(appid2,facetid,=chlg,=tlsdata) = fcp in
    if xsdata = sdata && fc = ToFc(hash(fcp)) then
    if (aaid,fc,kid,cntr,pkAU) = checksign(s,pkAT) then
    (
        event RP_success_reg(uname,appid2,aaid,kid);
        out(c,debug)
    )	
).

let RegUS_1b_seta(SR:channel, appid:Appid) = RegUS_seta(SR, appid).
let RegUS_1b_noa(SR:channel, appid:Appid) = RegUS_noa(SR, appid).
let RegUS_2b_seta(SR:channel, appid:Appid) = RegUS_seta(SR, appid).
let RegUS_2b_noa(SR:channel, appid:Appid) = RegUS_noa(SR, appid).
let RegUS_1r_seta(SR:channel, appid:Appid) = RegUS_seta(SR, appid).
let RegUS_1r_noa(SR:channel, appid:Appid) = RegUS_noa(SR, appid).
let RegUS_2r_seta(SR:channel, appid:Appid) = RegUS_seta(SR, appid).
let RegUS_2r_noa(SR:channel, appid:Appid) = RegUS_noa(SR, appid).

(********************************************************************)
(*                                            Relying Party                                                 *)
(********************************************************************)
let RegRP_seta(RS:channel, https:channel, uname:Uname, password:bitstring) =
(
	in(https,up:msg);
	let InputUandP(=uname, =password) = up in
    let tlsdata = GetTLSdata(https) in
    out(RS,RegRSbegin(uname, tlsdata));
    in(RS,m:msg);
    let RegSRreq(uname,appid,sdata,chlg) = m in
    out(https,RegRUreq(uname,appid,sdata,chlg));
    in(https,x:msg);
    let RegURresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s) = x in
    out(RS, RegRSresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s))
).

let RegRP_noa(RS:channel, https:channel, uname:Uname, password:bitstring) =
(
	in(https,up:msg);
	let InputUandP(=uname, =password) = up in
    let tlsdata = GetTLSdata(https) in
    out(RS,RegRSbegin(uname, tlsdata));
    in(RS,m:msg);
    let RegSRreq_null(uname,sdata,chlg) = m in
    out(https,RegRUreq_null(uname,sdata,chlg));
    in(https,x:msg);
    let RegURresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s) = x in
    out(RS, RegRSresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s))
).

let RegRP_1b_seta(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_seta(RS, https, uname, password).
let RegRP_1b_noa(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_noa(RS, https, uname, password).
let RegRP_2b_seta(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_seta(RS, https, uname, password).
let RegRP_2b_noa(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_noa(RS, https, uname, password).
let RegRP_1r_seta(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_seta(RS, https, uname, password).
let RegRP_1r_noa(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_noa(RS, https, uname, password).
let RegRP_2r_seta(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_seta(RS, https, uname, password).
let RegRP_2r_noa(RS:channel, https:channel, uname:Uname, password:bitstring) = RegRP_noa(RS, https, uname, password).

(********************************************************************)
(*                                             User Agent                                                   *)
(* 1.  only represent a honest user agent                     					                *)
(* 2. it can visit a fake website                                                                       *)
(********************************************************************)
let RegUA_seta(https:channel,UC:channel,uname:Uname,password:bitstring) =
(
	event UA_init_reg(uname);
	out(https,InputUandP(uname,password));
	in(https, m:msg);
    let RegRUreq(xuname,appid,sdata,chlg) = m in
    (
        let tlsdata = GetTLSdata(https) in
        out(UC,RegUCreq(xuname,appid,sdata,chlg,tlsdata));
        in(UC, mm:msg);
        let RegCUresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s)  = mm in
        out(https,RegURresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s))
    )
).

let RegUA_noa(https:channel,UC:channel,uname:Uname,password:bitstring) =
(
	event UA_init_reg(uname);
	out(https,InputUandP(uname,password));
	in(https, m:msg);
    let RegRUreq_null(xuname,sdata,chlg) = m in
    (
        let tlsdata = GetTLSdata(https) in
        out(UC,RegUCreq_null(xuname,sdata,chlg,tlsdata));
        in(UC, mm:msg);
        let RegCUresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s)  = mm in
        out(https,RegURresp(xsdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s))
    )
).

let RegUA_1b_seta(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_seta(https,UC,uname,password).
let RegUA_1b_noa(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_noa(https,UC,uname,password).
let RegUA_2b_seta(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_seta(https,UC,uname,password).
let RegUA_2b_noa(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_noa(https,UC,uname,password).
let RegUA_1r_seta(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_seta(https,UC,uname,password).
let RegUA_1r_noa(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_noa(https,UC,uname,password).
let RegUA_2r_seta(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_seta(https,UC,uname,password).
let RegUA_2r_noa(https:channel,UC:channel,uname:Uname,password:bitstring) = RegUA_noa(https,UC,uname,password).

(********************************************************************)
(*                                             FIDO Client                                                  *)
(* 1. FIDO UAF client                   				                                                    *)
(********************************************************************)
let RegUC_seta(CU:channel,CM:channel,facetid:Facetid) =
(
	in(CU,m:msg);
    let RegUCreq(uname,appid,sdata,chlg,tlsdata) = m in
    get AppList(=appid, =facetid) in
    let fcp = FCParams(appid,facetid,chlg,tlsdata) in
    out(CM,RegCMreq(uname,appid,fcp));
    in(CM,mm:msg);
    let RegMCresp(aaid,fc,kid,cntr,pkAT,pkAU,s) = mm in
    out(CU,RegCUresp(sdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s))
).

let RegUC_noa(CU:channel,CM:channel,facetid:Facetid) =
(
	in(CU,m:msg);
    let RegUCreq_null(uname,sdata,chlg,tlsdata) = m in
    let appid = facetid_to_appid(facetid) in
    let fcp = FCParams(appid,facetid,chlg,tlsdata) in
    out(CM,RegCMreq(uname,appid,fcp));
    in(CM,mm:msg);
    let RegMCresp(aaid,fc,kid,cntr,pkAT,pkAU,s) = mm in
    out(CU,RegCUresp(sdata,aaid,fc,kid,cntr,pkAT,pkAU,fcp,s))
).

let RegUC_1b_seta(CU:channel,CM:channel,facetid:Facetid) = RegUC_seta(CU,CM,facetid).
let RegUC_1b_noa(CU:channel,CM:channel,facetid:Facetid) = RegUC_noa(CU,CM,facetid).
let RegUC_2b_seta(CU:channel,CM:channel,facetid:Facetid) = RegUC_seta(CU,CM,facetid).
let RegUC_2b_noa(CU:channel,CM:channel,facetid:Facetid) = RegUC_noa(CU,CM,facetid).
let RegUC_1r_seta(CU:channel,CM:channel,facetid:Facetid) = RegUC_seta(CU,CM,facetid).
let RegUC_1r_noa(CU:channel,CM:channel,facetid:Facetid) = RegUC_noa(CU,CM,facetid).
let RegUC_2r_seta(CU:channel,CM:channel,facetid:Facetid) = RegUC_seta(CU,CM,facetid).
let RegUC_2r_noa(CU:channel,CM:channel,facetid:Facetid) = RegUC_noa(CU,CM,facetid).


(********************************************************************)
(*                                                  ASM                                                        *)
(********************************************************************)
let RegASM_1b2b(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) =
(
	in(MC, m:msg);
	let RegCMreq(uname,appid,fcp) = m in
	let fc = ToFc(hash(fcp)) in
    (* bound authenticator send keyhandle and store it into the ASM *)
    let ak = To_12b_token(appid,token,callerid,personaid) in
    let testak = ak in
    out(MA,RegMAreq(uname,appid,ak,fc));
    in(MA,mm:msg);
    let RegAMresp_12b(aaid,fc2,kid,cntr,pkAT,pkAU,kh,s) = mm in
    out(MC,RegMCresp(aaid,fc2,kid,cntr,pkAT,pkAU,s))
).

let RegASM_1r2r(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) =
(
	in(MC, m:msg);
	let RegCMreq(uname,appid,fcp) = m in
	let fc = ToFc(hash(fcp)) in
    let ak = To_12r_token(appid) in
    let testak = ak in
    (* roaming authenticators don't send the keyhandle *)
    out(MA,RegMAreq(uname,appid,ak,fc));
    in(MA,mm:msg);
    let RegAMresp_12r(aaid,fc2,kid,cntr,pkAT,pkAU,s) = mm in
    out(MC,RegMCresp(aaid,fc2,kid,cntr,pkAT,pkAU,s))
).

let RegASM_1b_seta(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1b2b(MC, MA, token, callerid, personaid).
let RegASM_1b_noa(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1b2b(MC, MA, token, callerid, personaid).
let RegASM_2b_seta(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1b2b(MC, MA, token, callerid, personaid).
let RegASM_2b_noa(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1b2b(MC, MA, token, callerid, personaid).
let RegASM_1r_seta(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1r2r(MC, MA, token, callerid, personaid).
let RegASM_1r_noa(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1r2r(MC, MA, token, callerid, personaid).
let RegASM_2r_seta(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1r2r(MC, MA, token, callerid, personaid).
let RegASM_2r_noa(MC:channel, MA:channel, token:bitstring, callerid:Callerid, personaid:PersonaID) = RegASM_1r2r(MC, MA, token, callerid, personaid).


(********************************************************************)
(*                                             Authenticator                                               *)
(* 1. RegServer to registration                                                                      *)
(* 2. AuthServer to authentication                                                                *)
(* Both of them use the same channel                                                           *)
(********************************************************************)
let RegAutr_1b(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =
(
	in(AM,m:msg);
	let RegMAreq(uname,appid,ak,fc) = m in
	new skAU_seed:bitstring;
    let skAU = gen_skAU(skAU_basic,skAU_seed) in
	new cntr_seed:bitstring;
    let cntr = gen_cntr(cntr_basic,cntr_seed) in
	let pkAT = spk(skAT) in
	let pkAU = spk(skAU) in
    new kid_seed:bitstring;
    let kid = gen_kid(kid_basic, kid_seed) in
    let kh = senc((skAU,ak,uname,kid),wrapkey) in 
    let s = sign((aaid,fc,kid,cntr,pkAU),skAT) in
    event Autr_verify_reg(uname,appid,aaid,kid);
    out(AM,RegAMresp_12b(aaid,fc,kid,cntr,pkAT,pkAU,kh,s))
).

let RegAutr_2b(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =
(
	in(AM,m:msg);
	let RegMAreq(uname,appid,ak,fc) = m in
	new skAU_seed:bitstring;
    let skAU = gen_skAU(skAU_basic,skAU_seed) in
	new cntr_seed:bitstring;
    let cntr = gen_cntr(cntr_basic,cntr_seed) in
	let pkAT = spk(skAT) in
	let pkAU = spk(skAU) in
    new kid_seed:bitstring;
    let kid = gen_kid(kid_basic, kid_seed) in
    let kh = senc((skAU,ak,kid),wrapkey) in
    let s = sign((aaid,fc,kid,cntr,pkAU),skAT) in
    event Autr_verify_reg(uname,appid,aaid,kid);
    out(AM,RegAMresp_12b(aaid,fc,kid,cntr,pkAT,pkAU,kh,s))
).

let RegAutr_1r(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =
(
	in(AM,m:msg);
	let RegMAreq(uname,appid,ak,fc) = m in
	new skAU_seed:bitstring;
    let skAU = gen_skAU(skAU_basic,skAU_seed) in
	new cntr_seed:bitstring;
    let cntr = gen_cntr(cntr_basic,cntr_seed) in
	let pkAT = spk(skAT) in
	let pkAU = spk(skAU) in
     new kid_seed:bitstring;
    let kid = gen_kid(kid_basic, kid_seed) in
    let kh = senc((skAU,ak,uname,kid),wrapkey) in 
    let s = sign((aaid,fc,kid,cntr,pkAU),skAT) in
    event Autr_verify_reg(uname,appid,aaid,kid);
    out(AM,RegAMresp_12r(aaid,fc,kid,cntr,pkAT,pkAU,s))
).

let RegAutr_2r(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =
(
	in(AM,m:msg);
	let RegMAreq(uname,appid,ak,fc) = m in
	new skAU_seed:bitstring;
    let skAU = gen_skAU(skAU_basic,skAU_seed) in
	new cntr_seed:bitstring;
    let cntr = gen_cntr(cntr_basic,cntr_seed) in
	let pkAT = spk(skAT) in
	let pkAU = spk(skAU) in
    let kh = senc((skAU,ak),wrapkey) in
    let kid = kh in
    let s = sign((aaid,fc,kid,cntr,pkAU),skAT) in
    event Autr_verify_reg(uname,appid,aaid,kid);
    out(AM,RegAMresp_12r(aaid,fc,kid,cntr,pkAT,pkAU,s))
).

let RegAutr_1b_seta(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_1b(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_1b_noa(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_1b(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_2b_seta(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_2b(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_2b_noa(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_2b(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_1r_seta(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_1r(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_1r_noa(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_1r(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_2r_seta(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_2r(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).
let RegAutr_2r_noa(AM:channel,aaid:AAID,skAT:sskey,wrapkey:key,skAU_basic:sskey,cntr_basic:CNTR,kid_basic:KeyID) =  RegAutr_2r(AM,aaid,skAT,wrapkey,skAU_basic,cntr_basic,kid_basic).

(**************************message format 2*******************************)
fun AuthSRreq_em(Appid,SData,Chlg):msg[data].
fun AuthSRreq_em_null(SData,Chlg):msg[data].
fun AuthRSresp_em(SData,AAID,Nonce,Fchlg,KeyID,CNTR,bitstring,bitstring):msg[data].
fun AuthSRreq_st(Appid,KeyID,SData,Chlg,Tr):msg[data].
fun AuthSRreq_st_null(KeyID,SData,Chlg,Tr):msg[data].
fun AuthRSresp_st(SData,AAID,Nonce,Fchlg,Tr,KeyID,CNTR,bitstring,bitstring):msg[data].
fun AuthRUreq_em(Appid,SData,Chlg):msg[data].
fun AuthRUreq_em_null(SData,Chlg):msg[data].
fun AuthURresp_em(SData,AAID,Nonce,Fchlg,KeyID,CNTR,bitstring,bitstring):msg[data].
fun AuthRUreq_st(Appid,KeyID,SData,Chlg,Tr):msg[data].
fun AuthRUreq_st_null(KeyID,SData,Chlg,Tr):msg[data].
fun AuthURresp_st(SData,AAID,Nonce,Fchlg,Tr,KeyID,CNTR,bitstring,bitstring):msg[data].
fun AuthUCreq_em(Appid,SData,Chlg,bitstring):msg[data].
fun AuthUCreq_em_null(SData,Chlg,bitstring):msg[data].
fun AuthCUresp_em(SData,AAID,Nonce,Fchlg,KeyID,CNTR,bitstring,bitstring):msg[data].
fun AuthUCreq_st(Appid,KeyID,SData,Chlg,Tr,bitstring):msg[data].
fun AuthUCreq_st_null(KeyID,SData,Chlg,Tr,bitstring):msg[data].
fun AuthCUresp_st(SData,AAID,Nonce,Fchlg,Tr,KeyID,CNTR,bitstring,bitstring):msg[data].
fun AuthCMreq_em(Appid,bitstring):msg[data].
fun AuthMCresp_em(AAID,Nonce,Fchlg,KeyID,CNTR,bitstring):msg[data].
fun AuthCMreq_st(Appid,bitstring,KeyID,Tr):msg[data].
fun AuthMCresp_st(AAID,Nonce,Fchlg,Tr,KeyID,CNTR,bitstring):msg[data].
fun AuthMAreq_1bem(Token,Fchlg,Appid,KeyID):msg[data].
fun AuthMAreq_1bst(Token,Fchlg,Appid,KeyID,Tr):msg[data].
fun AuthMAreq_2bst(Token,Fchlg,Appid,KeyID,Tr):msg[data].
fun AuthMAreq_1rem(Token,Fchlg,Appid):msg[data].
fun AuthMAreq_1rst(Token,Fchlg,Appid,KeyID,Tr):msg[data].
fun AuthMAreq_2rst(Token,Fchlg,Appid,KeyID,Tr):msg[data].
fun AuthAMresp_em(AAID,Nonce,Fchlg,KeyID,CNTR,bitstring):msg[data].
fun AuthAMresp_st(AAID,Nonce,Fchlg,Tr,KeyID,CNTR,bitstring):msg[data].

(********************************************************************)
(*                                               Event                                                          *)
(********************************************************************)

event RP_success_auth(Uname,Appid,AAID,KeyID).
event RP_success_tr(Tr).
event UA_launch_auth(Uname).
event UA_launch_auth_tr(Tr).
event Autr_verify_auth_1br(Uname,Appid,AAID,KeyID).
event Autr_verify_auth_2br(Appid,AAID,KeyID).
event Autr_verify_tr(Tr).

const ok:bitstring.

(********************************************************************)
(**************************main process*******************************)
(********************************************************************)
(********************************************************************)
(*                                                  Auth US                                                        *)
(********************************************************************)
let AuthUS_login_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) =
(
    in(SR, tlsdata:bitstring);
    new sdata:SData;
	new chlg:Chlg;
    out(SR,AuthSRreq_em(appid,sdata,chlg));
    in(SR,m:msg);
    let AuthRSresp_em(=sdata,=aaid,nonce,fc,=kid,xcntr,fcp,s) = m in
    let FCParams(appid,facetid,=chlg,=tlsdata) = fcp in
    get AuthAppList(=appid,=facetid) in
    if fc = ToFc(hash(fcp)) then
    if (aaid,nonce,fc,kid,xcntr) = checksign(s,pkAU) then
    if xcntr = cntr then
    event RP_success_auth(uname,appid,aaid,kid);
    out(c,debug)
 ).
 
let AuthUS_login_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) =
(
    in(SR, tlsdata:bitstring);
    new sdata:SData;
	new chlg:Chlg;
    out(SR,AuthSRreq_em_null(sdata,chlg));
    in(SR,m:msg);
    let AuthRSresp_em(=sdata,=aaid,nonce,fc,=kid,xcntr,fcp,s) = m in
    let FCParams(appid2,facetid,=chlg,=tlsdata) = fcp in
    get AuthAppList(=appid2,=facetid) in
    if fc = ToFc(hash(fcp)) then
    if (aaid,nonce,fc,kid,xcntr) = checksign(s,pkAU) then
    if xcntr = cntr then
    event RP_success_auth(uname,appid2,aaid,kid);
    out(c,debug)
 ).
 
 let AuthUS_stepup_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) =
(
    in(SR, tlsdata:bitstring);
    new sdata:SData;
	new chlg:Chlg;
     out(SR,AuthSRreq_st(appid,kid,sdata,chlg,tr));
    in(SR,m:msg);
    let AuthRSresp_st(=sdata,=aaid,nonce,fc,htr,=kid,xcntr,fcp,s) = m in
    let FCParams(=appid,facetid,=chlg,=tlsdata) = fcp in
    get AuthAppList(=appid,=facetid) in
    if fc = ToFc(hash(fcp)) then
    if htr = hash_tr(tr) then
    if (aaid,nonce,fc,htr,kid,xcntr) = checksign(s,pkAU) then
    if xcntr = cntr then
    event RP_success_auth(uname,appid,aaid,kid);
    event RP_success_tr(tr);
    out(c,debug)
 ).

 
let AuthUS_stepup_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) =
(
    in(SR, tlsdata:bitstring);
    new sdata:SData;
	new chlg:Chlg;
    out(SR,AuthSRreq_st_null(kid,sdata,chlg,tr));
    in(SR,m:msg);
    let AuthRSresp_st(=sdata,=aaid,nonce,fc,htr,=kid,xcntr,fcp,s) = m in
    let FCParams(appid2,facetid,=chlg,=tlsdata) = fcp in
    get AuthAppList(=appid2,=facetid) in
    if fc = ToFc(hash(fcp)) then
    if htr = hash_tr(tr) then
    if (aaid,nonce,fc,htr,kid,xcntr) = checksign(s,pkAU) then
    if xcntr = cntr then
    event RP_success_auth(uname,appid2,aaid,kid);
    event RP_success_tr(tr);
    out(c,debug)
 ).
 
let AuthUS_1b_login_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_login_seta(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1b_login_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_login_noa(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1b_stepup_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_seta(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1b_stepup_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_noa(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_2b_stepup_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_seta(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_2b_stepup_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_noa(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1r_login_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_login_seta(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1r_login_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_login_noa(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1r_stepup_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_seta(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_1r_stepup_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_noa(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_2r_stepup_seta(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_seta(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
let AuthUS_2r_stepup_noa(SR:channel, uname:Uname, appid:Appid, aaid:AAID, kid:KeyID, pkAU:spkey, cntr:CNTR, tr:Tr) = AuthUS_stepup_noa(SR, uname,appid,aaid,kid,pkAU,cntr,tr).
	
(********************************************************************)
(*                                                  AuthRP                                                        *)
(********************************************************************)
    
let AuthRP_login_seta(RS:channel,https:channel) = 
(
    in(https, tg:bitstring);
    let tlsdata = GetTLSdata(https) in
    out(RS, tlsdata);
    in(RS,m:msg);
    let AuthSRreq_em(appid,sdata,chlg) = m in
    out(https,AuthRUreq_em(appid,sdata,chlg));
    in(https,x:msg);
    let AuthURresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s) = x in 
    out(RS, AuthRSresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s))
).

let AuthRP_login_noa(RS:channel,https:channel) = 
(
    in(https, tg:bitstring);
    let tlsdata = GetTLSdata(https) in
    out(RS, tlsdata);
    in(RS,m:msg);
    let AuthSRreq_em_null(sdata,chlg) = m in
    out(https,AuthRUreq_em_null(sdata,chlg));
    in(https,x:msg);
    let AuthURresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s) = x in 
    out(RS, AuthRSresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s))
).
let AuthRP_stepup_seta(RS:channel,https:channel) = 
(
    in(https, tg:bitstring);
    let tlsdata = GetTLSdata(https) in
    out(RS, tlsdata);
    in(RS,m:msg);
    let AuthSRreq_st(appid,kid,sdata,chlg,tr) = m in
    out(https,AuthRUreq_st(appid,kid,sdata,chlg,tr));
    in(https,x:msg);
    let AuthURresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s) = x in 
    out(RS, AuthRSresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s))
).
let AuthRP_stepup_noa(RS:channel,https:channel) = 
(
    in(https, tg:bitstring);
    let tlsdata = GetTLSdata(https) in
    out(RS, tlsdata);
    in(RS,m:msg);
    let AuthSRreq_st_null(kid,sdata,chlg,tr) = m in
    out(https,AuthRUreq_st_null(kid,sdata,chlg,tr));
    in(https,x:msg);
    let AuthURresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s) = x in 
    out(RS, AuthRSresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s))
).

let AuthRP_1b_login_seta(RS:channel,https:channel) = AuthRP_login_seta(RS,https).
let AuthRP_1b_login_noa(RS:channel,https:channel) = AuthRP_login_noa(RS,https).
let AuthRP_1b_stepup_seta(RS:channel,https:channel) = AuthRP_stepup_seta(RS,https).
let AuthRP_1b_stepup_noa(RS:channel,https:channel) = AuthRP_stepup_noa(RS,https).
let AuthRP_2b_stepup_seta(RS:channel,https:channel) = AuthRP_stepup_seta(RS,https).
let AuthRP_2b_stepup_noa(RS:channel,https:channel) = AuthRP_stepup_noa(RS,https).
let AuthRP_1r_login_seta(RS:channel,https:channel) = AuthRP_login_seta(RS,https).
let AuthRP_1r_login_noa(RS:channel,https:channel) = AuthRP_login_noa(RS,https).
let AuthRP_1r_stepup_seta(RS:channel,https:channel) = AuthRP_stepup_seta(RS,https).
let AuthRP_1r_stepup_noa(RS:channel,https:channel) = AuthRP_stepup_noa(RS,https).
let AuthRP_2r_stepup_seta(RS:channel,https:channel) = AuthRP_stepup_seta(RS,https).
let AuthRP_2r_stepup_noa(RS:channel,https:channel) = AuthRP_stepup_noa(RS,https).


(********************************************************************)
(*                                                  AuthUA                                                        *)
(********************************************************************)

let AuthUA_login_seta(https:channel,UC:channel,uname:Uname) =
(
    out(https,ok);
	in(https,m:msg);
    let AuthRUreq_em(appid,sdata,chlg) = m in
    event UA_launch_auth(uname);
    let tlsdata = GetTLSdata(https) in
    out(UC,AuthUCreq_em(appid,sdata,chlg,tlsdata));
    in(UC,mm:msg);
    let AuthCUresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s) = mm in
    out(https,AuthURresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s))
 ).
let AuthUA_stepup_seta(https:channel,UC:channel,uname:Uname) =
(
    out(https,ok);
	in(https,m:msg);
    let AuthRUreq_st(appid,kid,sdata,chlg,tr2) = m in
    event UA_launch_auth(uname);
    event UA_launch_auth_tr(tr2);
    let tlsdata = GetTLSdata(https) in
    out(UC,AuthUCreq_st(appid,kid,sdata,chlg,tr2,tlsdata));
    in(UC,mm:msg);
    let AuthCUresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s) = mm in
    out(https,AuthURresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s))
 ).
let AuthUA_login_noa(https:channel,UC:channel,uname:Uname) =
(
    out(https,ok);
	in(https,m:msg);
    let AuthRUreq_em_null(sdata,chlg) = m in
    event UA_launch_auth(uname);
    let tlsdata = GetTLSdata(https) in
    out(UC,AuthUCreq_em_null(sdata,chlg,tlsdata));
    in(UC,mm:msg);
    let AuthCUresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s) = mm in
    out(https,AuthURresp_em(xsdata,aaid,nonce,fc,kid,xcntr,fcp,s))
 ).
 
let AuthUA_stepup_noa(https:channel,UC:channel,uname:Uname) =
(
    out(https,ok);
	in(https,m:msg);
    let AuthRUreq_st_null(kid,sdata,chlg,tr2) = m in
    event UA_launch_auth(uname);
    event UA_launch_auth_tr(tr2);
    let tlsdata = GetTLSdata(https) in
    out(UC,AuthUCreq_st_null(kid,sdata,chlg,tr2,tlsdata));
    in(UC,mm:msg);
    let AuthCUresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s) = mm in
    out(https,AuthURresp_st(xsdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s))
 ).
 
let AuthUA_1b_login_seta(https:channel,UC:channel,uname:Uname) = AuthUA_login_seta(https,UC,uname).
let AuthUA_1b_login_noa(https:channel,UC:channel,uname:Uname) = AuthUA_login_noa(https,UC,uname).
let AuthUA_1b_stepup_seta(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_seta(https,UC,uname).
let AuthUA_1b_stepup_noa(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_noa(https,UC,uname).
let AuthUA_2b_stepup_seta(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_seta(https,UC,uname).
let AuthUA_2b_stepup_noa(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_noa(https,UC,uname).
let AuthUA_1r_login_seta(https:channel,UC:channel,uname:Uname) = AuthUA_login_seta(https,UC,uname).
let AuthUA_1r_login_noa(https:channel,UC:channel,uname:Uname) = AuthUA_login_noa(https,UC,uname).
let AuthUA_1r_stepup_seta(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_seta(https,UC,uname).
let AuthUA_1r_stepup_noa(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_noa(https,UC,uname).
let AuthUA_2r_stepup_seta(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_seta(https,UC,uname).
let AuthUA_2r_stepup_noa(https:channel,UC:channel,uname:Uname) = AuthUA_stepup_noa(https,UC,uname).

(********************************************************************)
(*                                                  AuthUC                                                        *)
(********************************************************************)

let AuthUC_login_seta(CU:channel,CM:channel,facetid:Facetid) =
(
	in(CU,m:msg);
     let AuthUCreq_em(appid,sdata,chlg,tlsdata) = m in
    get AuthAppList(=appid,=facetid) in
    let fcp = FCParams(appid,facetid,chlg,tlsdata) in
    out(CM,AuthCMreq_em(appid,fcp));
    in(CM,mm:msg);
    let AuthMCresp_em(aaid,nonce,fc,kid,xcntr,s) = mm in
    out(CU,AuthCUresp_em(sdata,aaid,nonce,fc,kid,xcntr,fcp,s))
).

let AuthUC_stepup_seta(CU:channel,CM:channel,facetid:Facetid) =
(
	in(CU,m:msg);
     let AuthUCreq_st(appid,kid,sdata,chlg,tr,tlsdata) = m in
    get AuthAppList(=appid,=facetid) in
    let fcp = FCParams(appid,facetid,chlg,tlsdata) in
    out(CM,AuthCMreq_st(appid,fcp,kid,tr));
    in(CM,mm:msg);
    let AuthMCresp_st(aaid,nonce,fc,htr,xkid,xcntr,s) = mm in
    out(CU,AuthCUresp_st(sdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s))
).

let AuthUC_login_noa(CU:channel,CM:channel,facetid:Facetid) =
(
	in(CU,m:msg);
    let AuthUCreq_em_null(sdata,chlg,tlsdata) = m in
    let appid = facetid_to_appid(facetid) in
    let fcp = FCParams(appid,facetid,chlg,tlsdata) in
    out(CM,AuthCMreq_em(appid,fcp));
    in(CM,mm:msg);
    let AuthMCresp_em(aaid,nonce,fc,kid,xcntr,s) = mm in
    out(CU,AuthCUresp_em(sdata,aaid,nonce,fc,kid,xcntr,fcp,s))
).

let AuthUC_stepup_noa(CU:channel,CM:channel,facetid:Facetid) =
(
	in(CU,m:msg);
    let AuthUCreq_st_null(kid,sdata,chlg,tr,tlsdata) = m in
    let appid = facetid_to_appid(facetid) in
    let fcp = FCParams(appid,facetid,chlg,tlsdata) in
    out(CM,AuthCMreq_st(appid,fcp,kid,tr));
    in(CM,mm:msg);
    let AuthMCresp_st(aaid,nonce,fc,htr,xkid,xcntr,s) = mm in
    out(CU,AuthCUresp_st(sdata,aaid,nonce,fc,htr,xkid,xcntr,fcp,s))
).

let AuthUC_1b_login_seta(CU:channel,CM:channel,facetid:Facetid) = AuthUC_login_seta(CU,CM,facetid).
let AuthUC_1b_login_noa(CU:channel,CM:channel,facetid:Facetid) = AuthUC_login_noa(CU,CM,facetid).
let AuthUC_1b_stepup_seta(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_seta(CU,CM,facetid).
let AuthUC_1b_stepup_noa(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_noa(CU,CM,facetid).
let AuthUC_2b_stepup_seta(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_seta(CU,CM,facetid).
let AuthUC_2b_stepup_noa(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_noa(CU,CM,facetid).
let AuthUC_1r_login_seta(CU:channel,CM:channel,facetid:Facetid) = AuthUC_login_seta(CU,CM,facetid).
let AuthUC_1r_login_noa(CU:channel,CM:channel,facetid:Facetid) = AuthUC_login_noa(CU,CM,facetid).
let AuthUC_1r_stepup_seta(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_seta(CU,CM,facetid).
let AuthUC_1r_stepup_noa(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_noa(CU,CM,facetid).
let AuthUC_2r_stepup_seta(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_seta(CU,CM,facetid).
let AuthUC_2r_stepup_noa(CU:channel,CM:channel,facetid:Facetid) = AuthUC_stepup_noa(CU,CM,facetid).

(********************************************************************)
(*                                                  AuthASM                                                        *)
(********************************************************************)

let AuthASM_1b_login(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) =
(
	in(MC,m:msg);
    let AuthCMreq_em(appid,fcp) = m in
    get ASMDB(=appid,kid,kh) in
    get TrustCallerid(=callerid) in
    let fc = ToFc(hash(fcp)) in
    let ak = To_12b_token(appid,token,callerid,personaid) in 
    let testak = ak in
    out(MA,AuthMAreq_1bem(ak,fc,appid,kh));
    in(MA,mm:msg);
    let AuthAMresp_em(aaid,nonce,xfc,xkid,xcntr,s) = mm in
    out(MC,AuthMCresp_em(aaid,nonce,xfc,xkid,xcntr,s))
).
let AuthASM_1b_stepup(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) =
(
	in(MC,m:msg);
    let AuthCMreq_st(appid,fcp,kid,tr) = m in
    get ASMDB(appid2,=kid,kh) in
    get TrustCallerid(=callerid) in
    let fc = ToFc(hash(fcp)) in
    let ak = To_12b_token(appid,token,callerid,personaid) in 
    let testak = ak in
    out(MA,AuthMAreq_1bst(ak,fc,appid,kh,tr));
    in(MA,mm:msg);
    let AuthAMresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s) = mm in
    out(MC,AuthMCresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s))
).
let AuthASM_2b_stepup(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) =
(
	in(MC,m:msg);
    let AuthCMreq_st(appid,fcp,kid,tr) = m in
    get ASMDB(=appid,=kid,kh) in
    get TrustCallerid(=callerid) in
    let fc = ToFc(hash(fcp)) in
    let ak = To_12b_token(appid,token,callerid,personaid) in 
    let testak = ak in
    out(MA,AuthMAreq_2bst(ak,fc,appid,kh,tr));
    in(MA,mm:msg);
    let AuthAMresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s) = mm in
    out(MC,AuthMCresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s))
).
let AuthASM_1r_login(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) =
(
	in(MC,m:msg);
    let AuthCMreq_em(appid,fcp) = m in
    let fc = ToFc(hash(fcp)) in
    let ak = To_12r_token(appid) in
    let testak = ak in
    out(MA,AuthMAreq_1rem(ak,fc,appid));
    in(MA,mm:msg);
    let AuthAMresp_em(aaid,nonce,xfc,kid,xcntr,s) = mm in
    out(MC,AuthMCresp_em(aaid,nonce,xfc,kid,xcntr,s))
).
let AuthASM_1r_stepup(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) =
(
	in(MC,m:msg);
    let AuthCMreq_st(appid,fcp,kid,tr) = m in
    let fc = ToFc(hash(fcp)) in
    let ak = To_12r_token(appid) in
    let testak = ak in
    out(MA,AuthMAreq_1rst(ak,fc,appid,kid,tr));
    in(MA,mm:msg);
    let AuthAMresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s) = mm in
    out(MC,AuthMCresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s))
).
let AuthASM_2r_stepup(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) =
(
	in(MC,m:msg);
    let AuthCMreq_st(appid,fcp,kid,tr) = m in
    let fc = ToFc(hash(fcp)) in
    let ak = To_12r_token(appid) in
    let testak = ak in
    out(MA,AuthMAreq_2rst(ak,fc,appid,kid,tr));
    in(MA,mm:msg);
    let AuthAMresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s) = mm in
    out(MC,AuthMCresp_st(aaid,nonce,xfc,htr,xkid,xcntr,s))
).

let AuthASM_1b_login_seta(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1b_login(MC,MA,token,callerid,personaid).
let AuthASM_1b_login_noa(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1b_login(MC,MA,token,callerid,personaid).
let AuthASM_1b_stepup_seta(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1b_stepup(MC,MA,token,callerid,personaid).
let AuthASM_1b_stepup_noa(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1b_stepup(MC,MA,token,callerid,personaid).
let AuthASM_2b_stepup_seta(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_2b_stepup(MC,MA,token,callerid,personaid).
let AuthASM_2b_stepup_noa(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_2b_stepup(MC,MA,token,callerid,personaid).
let AuthASM_1r_login_seta(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1r_login(MC,MA,token,callerid,personaid).
let AuthASM_1r_login_noa(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1r_login(MC,MA,token,callerid,personaid).
let AuthASM_1r_stepup_seta(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1r_stepup(MC,MA,token,callerid,personaid).
let AuthASM_1r_stepup_noa(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_1r_stepup(MC,MA,token,callerid,personaid).
let AuthASM_2r_stepup_seta(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_2r_stepup(MC,MA,token,callerid,personaid).
let AuthASM_2r_stepup_noa(MC:channel,MA:channel,token:bitstring,callerid:Callerid,personaid:PersonaID) = AuthASM_2r_stepup(MC,MA,token,callerid,personaid).

(********************************************************************)
(*                                               Auth Authenticator                                                   *)
(********************************************************************)

let AuthAutr_1b_login(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) =
(
	in(AM,m:msg);
    let AuthMAreq_1bem(ak,fc,appid,kh) = m in
    let (skAU:sskey,aka:Token,uname:Uname,kid:KeyID) = sdec(kh,wrapkey) in
    if f1(ak,appid) = aka then
    let pkAU = spk(skAU) in
    new nonce:Nonce;
    let s = sign((aaid,nonce,fc,kid,cntr),skAU) in
    event Autr_verify_auth_1br(uname,appid,aaid,kid);
    out(AM,AuthAMresp_em(aaid,nonce,fc,kid,cntr,s))
).
let AuthAutr_1b_stepup(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) =
(
	in(AM,m:msg);
    let AuthMAreq_1bst(ak,fc,appid,kh,=tr) = m in
    let (skAU:sskey,aka:Token,uname:Uname,kid:KeyID) = sdec(kh,wrapkey) in
    if f1(ak,appid) = aka then
    let pkAU = spk(skAU) in
    let htr = hash_tr(tr) in
    new nonce:Nonce;
    let s = sign((aaid,nonce,fc,htr,kid,cntr),skAU) in
    event Autr_verify_auth_1br(uname,appid,aaid,kid);
    event Autr_verify_tr(tr);
    out(AM,AuthAMresp_st(aaid,nonce,fc,htr,kid,cntr,s))
).
let AuthAutr_2b_stepup(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) =
(
	in(AM,m:msg);
    let AuthMAreq_2bst(ak,fc,appid,kh,=tr) = m in
    let (skAU:sskey,aka:Token,kid:KeyID) = sdec(kh,wrapkey) in
    if f1(ak,appid) = aka then
    let pkAU = spk(skAU) in
    let htr = hash_tr(tr) in
    new nonce:Nonce;
    let s = sign((aaid,nonce,fc,htr,kid,cntr),skAU) in
    event Autr_verify_auth_2br(appid,aaid,kid);
    event Autr_verify_tr(tr);
    out(AM,AuthAMresp_st(aaid,nonce,fc,htr,kid,cntr,s))
).
let AuthAutr_1r_login(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) =
(
	in(AM,m:msg);
    let AuthMAreq_1rem(ak,fc,appid) = m in
    get AutrDB(=appid,xkid,kh) in
    let (skAU:sskey,aka:Token,uname:Uname,kid:KeyID) = sdec(kh,wrapkey) in
    if f1(ak,appid) = aka then
    let pkAU = spk(skAU) in
    new nonce:Nonce;
    let s = sign((aaid,nonce,fc,kid,cntr),skAU) in 
    event Autr_verify_auth_1br(uname,appid,aaid,kid);
    out(AM,AuthAMresp_em(aaid,nonce,fc,kid,cntr,s))
).
let AuthAutr_1r_stepup(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) =
(
	in(AM,m:msg);
    let AuthMAreq_1rst(ak,fc,appid,kid,=tr) = m in
    get AutrDB(=appid,=kid,kh) in
    let (skAU:sskey,aka:Token,uname:Uname,xkid:KeyID) = sdec(kh,wrapkey) in
    if f1(ak,appid) = aka then
    let pkAU = spk(skAU) in
    let htr = hash_tr(tr) in
    new nonce:Nonce;
    let s = sign((aaid,nonce,fc,htr,kid,cntr),skAU) in
    event Autr_verify_auth_1br(uname,appid,aaid,kid);
    event Autr_verify_tr(tr);
    out(AM,AuthAMresp_st(aaid,nonce,fc,htr,kid,cntr,s))
).
let AuthAutr_2r_stepup(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) =
(
	in(AM,m:msg);
    let AuthMAreq_2rst(ak,fc,appid,kid,=tr) = m in
    let (skAU:sskey,aka:Token) = sdec(kid,wrapkey) in
    if f1(ak,appid) = aka then
    let pkAU = spk(skAU) in
    let htr = hash_tr(tr) in
    new nonce:Nonce;
    let s = sign((aaid,nonce,fc,htr,kid,cntr),skAU) in
    event Autr_verify_auth_2br(appid,aaid,kid);
    event Autr_verify_tr(tr);
    out(AM,AuthAMresp_st(aaid,nonce,fc,htr,kid,cntr,s))
).

let AuthAutr_1b_login_seta(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1b_login(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1b_login_noa(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1b_login(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1b_stepup_seta(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1b_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1b_stepup_noa(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1b_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_2b_stepup_seta(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_2b_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_2b_stepup_noa(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_2b_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1r_login_seta(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1r_login(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1r_login_noa(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1r_login(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1r_stepup_seta(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1r_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_1r_stepup_noa(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_1r_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_2r_stepup_seta(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_2r_stepup(AM,aaid,wrapkey,cntr,tr).
let AuthAutr_2r_stepup_noa(AM:channel,aaid:AAID,wrapkey:key,cntr:CNTR,tr:Tr) = AuthAutr_2r_stepup(AM,aaid,wrapkey,cntr,tr).
